MODULE my_fxn
   IMPLICIT NONE   
   PRIVATE
   PUBLIC ::  fxn_2, &
              M_D, nd   
   
   
   REAL(KIND(0D0)), PARAMETER      :: S=1.69d8                                ! S = (P1+P2)^2 which P1, P2 is four vector of protons 
   REAL(KIND(0D0)), PARAMETER      :: g_s = 0.118d0
   REAL(KIND(0d0))                 :: M_D                                     ! Plack energy in D dimensional
   INTEGER                         :: nd                                      ! nd is the number of integration 
   REAL(KIND(0D0)), PARAMETER      :: m=172d0                                 ! The mass of top quark
   REAL(KIND(0D0)), PARAMETER      :: G_QCDFactorazationScale=1d2             
   REAL(KIND(0D0)), PARAMETER      :: pi=3.14159d0
   REAL(KIND(0D0)), EXTERNAL       :: CT14pdf                            
   REAL(KIND(0d0)) :: s12                                                     ! s12 = x1*x2*S, which p1, p2 is total energy in parton reference frame.
   INTEGER         :: i                                                       ! i is loop index for subrountines
   CONTAINS        
      FUNCTION jacobian( upper, lower) RESULT(jfactor)
         IMPLICIT NONE
         REAL(KIND(0D0)), DIMENSION(1:nd) :: upper, lower
         REAL(KIND(0D0))  :: jfactor
          
         jfactor = 1d0
         DO i = 1, nd
            jfactor = jfactor * (upper(i) - lower(i))
         END DO
      END FUNCTION jacobian



      FUNCTION dot_vec(p,q) RESULT(fourvectordot)
         IMPLICIT NONE
         REAL(KIND(0d0)) :: fourvectordot
         REAL(KIND(0D0)), DIMENSION(0:3) :: p,q

         fourvectordot = p(0) * q(0)
         DO i = 1, 3
            fourvectordot = fourvectordot - p(i) * q(i)
         END DO
      END FUNCTION dot_vec
      
      

      SUBROUTINE commonpart(p3_0, p4_0,cos_theta,eta, k_v,P3_v, p4_v, s13, s14, s23, s24) ! The result generated by Mathematica is composed of s12,
                                                                                          ! s13, s14, s23, s24 etc.
         IMPLICIT NONE
         REAL(KIND(0D0)), INTENT(IN) :: p3_0, p4_0, cos_theta, eta, k_v, p3_v, p4_v 
         REAL(KIND(0D0)), INTENT(OUT):: s13, s14, s23, s24
         REAL(KIND(0d0)) :: sin_theta, &
                            cos_eta, sin_eta,   &
                            cos_ksi, sin_ksi
         REAL(KIND(0D0)), DIMENSION(0:3) :: k1, k2, p3, p4, k 


         sin_theta = SQRT(1-cos_theta**2) 
         cos_eta = COS(eta)
         sin_eta = SQRT(1-cos_eta**2)  
         cos_ksi = (k_v**2-p3_v**2-p4_v**2)/(2*p3_v*p4_v)
         sin_ksi = SQRT(1-cos_ksi**2)

         k1 = [SQRT(s12)/2d0,0d0,0d0, SQRT(s12)/2d0]
         k2 = [SQRT(s12)/2d0,0d0,0d0, -SQRT(s12)/2d0]
         p3 = [p3_0, p3_v*(cos_theta*cos_eta*sin_ksi+sin_theta*cos_ksi), &
               p3_v* sin_eta*sin_ksi, p3_v*( cos_theta*cos_ksi-sin_theta*cos_eta*sin_ksi)]
         p4 = [p4_0, p4_v*sin_theta, 0d0, p4_v*cos_theta]
         DO i = 1, 3
         k(i)  = 0 - p3(i) - p4(i)
         END DO
         k(0) = sqrt(s12) - p3_0-p4_0

         s13 = m**2- 2*dot_vec(k1,p3)  ! Defination of mandelstam variable in 2 to 3 particle scattering process
         s14 = m**2- 2*dot_vec(k1,p4)  ! where we leave off the mass of the initial particles.
         s23 = m**2- 2*dot_vec(k2,p3)
         s24 = m**2- 2*dot_vec(k2,p4)

      END SUBROUTINE commonpart

      FUNCTION fxn_2(z, wgt) RESULT(fxn_gg)
         IMPLICIT NONE 
         REAL(KIND(0D0)), DIMENSION(1:nd) :: z      
         REAL(KIND(0d0)) :: wgt
         REAL(KIND(0D0)) :: tau_0                             ! The variable τ characterizes the (invariant) mass of the 
                                                              ! reaction, with τ_0 = (m_res)^2/S      
         REAL(KIND(0d0)) :: sigma, tau, m_plus, m_minus,  &   ! intermediate var 
                            p3_v, p4_v, k_v, phi
         REAL(KIND(0D0)) :: s13,s14,s23, s24, gm, sunn    
         REAL(KIND(0D0)) :: part_gg,fxn_gg       
         REAL(KIND(0d0)) :: p3_0_max, p4_0_max, cos_theta_max, eta_max, gm_max, x1_max, x2_max, &
                            p3_0_min, p4_0_min, cos_theta_min, eta_min, gm_min, x1_min, x2_min
         REAL(KIND(0D0)), DIMENSION(1:nd) :: upper, lower
         REAL(KIND(0D0)) :: jfactor

         sunn = 3                                        
         wgt = 0

         gm_max = M_D
         gm_min = 0.1d0
         z(1)= (gm_max-gm_min)*z(1) + gm_min

         tau_0 = (2*m+z(1) )**2/S

         eta_max = 2*pi
         eta_min = 0
         z(2) = (eta_max-eta_min)*z(2)+eta_min

         cos_theta_max = 1
         cos_theta_min = -1
         z(3) = (cos_theta_max-cos_theta_min)*z(3)+cos_theta_min
          

         x1_max = 1
         x1_min = tau_0
         z(4) = (x1_max-x1_min)*z(4) + x1_min

         x2_max = 1
         x2_min = tau_0/z(4)
         z(5) = (x2_max-x2_min)*z(5)+x2_min

         s12 = z(4)*z(5) * S
         IF (SQRT(s12) < 2*m+z(1))THEN
            fxn_gg = 0d0 
            RETURN
            ELSE
         END IF

         p4_0_max = SQRT(s12)/2 - ((m+z(1))**2-m**2)/(2*SQRT(s12))
         p4_0_min = m
         z(6) = (p4_0_max-p4_0_min)*z(6)+p4_0_min

         p4_v = SQRT(z(6)**2-m**2) 
         sigma = SQRT(s12)-z(6)
         tau = sigma**2 - p4_v**2
         m_plus = m + z(1)
         m_minus = m - z(1)

         p3_0_max = 1/(2*tau)*(sigma*(tau+m_plus*m_minus)+p4_v*SQRT((tau-m_plus**2)*(tau-m_minus**2)))
         p3_0_min = 1/(2*tau)*(sigma*(tau+m_plus*m_minus)-p4_v-SQRT((tau-m_plus**2)*(tau-m_minus**2)))
         z(7) = (p3_0_max-p3_0_min)*z(7)+p3_0_min

         p3_v = SQRT(z(7)**2-m**2)  
         k_v = SQRT((SQRT(s12)-z(6)-z(7))**2-z(1)**2)

         gm = z(1)

         upper = [gm_max, eta_max, cos_theta_max, x1_max, x2_max, p4_0_max, p3_0_max]
         lower = [gm_min, eta_min, cos_theta_min, x1_min, x2_min, p4_0_min, p3_0_min]
         jfactor = jacobian(upper, lower)
         CALL commonpart(z(7),z(6),z(3),z(2), k_v,p3_v, p4_v, s13, s14, s23, s24) 

         INCLUDE "bingos4.m"

         part_gg = CT14Pdf(0,z(4),G_QCDFactorazationScale**2)*CT14Pdf(0,z(5),G_QCDFactorazationScale**2) * part_gg
         phi = 1/(8*(2*pi)**4) * 1/(2*s12)
         fxn_gg = jfactor*g_s**4/M_D**5*pi*z(1)**2*phi*part_gg
      END FUNCTION fxn_2
END MODULE my_fxn

